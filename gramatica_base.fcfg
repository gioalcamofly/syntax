% start S
# ###################
# Grammar Productions
# ###################
# S expansion productions
S[SEM=[ACCION=avanzar, CANTIDAD=?c, METRICA=?m]] -> V[ACCION=avanzar] SAdj[SEM=[CANTIDAD=?c, METRICA=?m]]
S[SEM=[ACCION=girar, CANTIDAD=?c, METRICA=grados]] -> V[ACCION=girar] SAdj[SEM=[CANTIDAD=?c, METRICA=grados]]

S[SEM=[ACCION=avanzar, CANTIDAD=?c, METRICA=?m, VELOCIDAD=?v]] -> V[ACCION=avanzar] SAdj[SEM=[CANTIDAD=?c, METRICA=?m]] SP[VELOCIDAD=?v]
S[SEM=[ACCION=avanzar, CANTIDAD=?c, METRICA=?m, VELOCIDAD=?v]] -> V[ACCION=avanzar] SP[VELOCIDAD=?v] SAdj[SEM=[CANTIDAD=?c, METRICA=?m]]

S[SEM=[ACCION=girar, DIRECCION=?d, CANTIDAD=?c, METRICA=grados]] -> V[ACCION=girar] SAdj[SEM=[CANTIDAD=?c, METRICA=grados]] SP[DIRECCION=?d]
S[SEM=[ACCION=girar, DIRECCION=?d, CANTIDAD=?c, METRICA=grados]] -> V[ACCION=girar] SP[DIRECCION=?d] SAdj[SEM=[CANTIDAD=?c, METRICA=grados]]

S[SEM=[ACCION=decir]] -> V[ACCION=decir] SN[SEM=[SUBCAT=estado]]


S[SEM=[ACCION=girar, DIRECCION=?d]] -> V[ACCION=girar] SP[DIRECCION=?d]

S[SEM=[ACCION=?a]] -> V[ACCION=?a]



SP[DIRECCION=?d] -> Prep Nominal-Direccion[SEM=[DIRECCION=?d]]
SP[VELOCIDAD=?v] -> Prep SN[SEM=[SUBCAT=velocidad, VELOCIDAD=?v]]

SN[SEM=[SUBCAT=velocidad, VELOCIDAD=[CANTIDAD=?c, METRICA=?m, TIEMPO=?t]]] -> Adj[CANTIDAD=?c] Nombre[METRICA=?m] Prep Nombre[TIEMPO=?t]
SN[SEM=[SUBCAT=estado]] -> Nominal[SEM=[TIPO=caracteristica]] Prep Nominal[SEM=[TIPO=componente]]

SAdj[SEM=[CANTIDAD=?c, METRICA=?m]] -> Adj[CANTIDAD=?c] Nombre[METRICA=?m]

Nominal-Direccion[SEM=[DIRECCION=?d]] -> Nombre-Direccion[DIRECCION=?d]
Nominal-Direccion[SEM=[DIRECCION=?d]] -> Det Nominal-Direccion[SEM=[DIRECCION=?d]]
Nominal[SEM=[TIPO=caracteristica]] -> Nombre[TIPO=caracteristica]
Nominal[SEM=[TIPO=caracteristica]] -> Det Nominal[SEM=[TIPO=caracteristica]]
Nominal[SEM=[TIPO=componente]] -> Nombre[TIPO=componente]
Nominal[SEM=[TIPO=componente]] -> Det Nominal[SEM=[TIPO=componente]]


# ###################
# Lexical Productions
# ###################

Prep -> 'a' | 'hacia' | 'por' | 'de'
Det -> 'el' | 'la' | 'mi' | 'tu' | 'su'

V[ACCION=conectarse] -> 'enciendete' | 'enciende' | 'conectate' | 'conecta' | 'activate' | 'activa'
V[ACCION=desconectarse] -> 'desconectate' | 'desconecta' |'desconectar' | 'apagate' | 'apaga' | 'apagar' | 'desactivate' | 'desactiva' | 'apagar'
V[ACCION=parar] -> 'parar' | 'parate' | 'para' | 'detente' | 'deten' | 'detener'
V[ACCION=avanzar] -> 'avanza' | 'avanzar' | 'camina' | 'caminar'
V[ACCION=girar] -> 'gira' | 'girar'
V[ACCION=decir] -> 'dime' | 'decir' | 'muestra' | 'mostrar' | 'muestrame'


Nombre[METRICA=centimetros] -> 'centimetro' | 'centimetros'
Nombre[METRICA=metros] -> 'metro' | 'metros'
Nombre[METRICA=grados] -> 'grado' | 'grados'
Nombre-Direccion[DIRECCION=derecha] -> 'derecha'
Nombre-Direccion[DIRECCION=izquierda] -> 'izquierda'
Nombre[TIEMPO=segundo] -> 'segundo' | 'segundos'
Nombre[TIEMPO=minuto] -> 'minuto' | 'minutos'
Nombre[TIEMPO=hora] -> 'hora' | 'horas'
Nombre[TIPO=caracteristica] -> 'estado' | 'situacion' | 'capacidad' | 'volumen'
Nombre[TIPO=componente] -> 'bateria' | 'pila' | 'memoria' | 'almacenamiento'

Adj[CANTIDAD=uno] -> 'un' | 'uno' | '1'
Adj[CANTIDAD=dos] -> 'dos' | '2'
Adj[CANTIDAD=tres] -> 'tres' | '3'
Adj[CANTIDAD=cuatro] -> 'cuatro' | '4'
Adj[CANTIDAD=cinco] -> 'cinco' | '5'
Adj[CANTIDAD=seis] -> 'seis' | '6'
Adj[CANTIDAD=siete] -> 'siete' | '7'
Adj[CANTIDAD=ocho] -> 'ocho' | '8'
Adj[CANTIDAD=nueve] -> 'nueve' | '9'
Adj[CANTIDAD=diez] -> 'diez' | '10'
